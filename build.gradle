buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'io.freefair.lombok' version '6.5.0.3'
    id 'java'
    id 'jacoco'
    id 'idea'
}

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/publish.gradle"

if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.local.gradle")
}

sourceCompatibility = 8
targetCompatibility = 8

ext {
    mcVersion = project.property("mcVersion")
}

group project.property("group")

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

archivesBaseName = project.property("pluginName")

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.codemc.io/repository/nms/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:${mcVersion}-R0.1-SNAPSHOT")

    // Add your dependencies here
    // Here are some opinionated dependencies that might help you with your plugin development:

    // Annotation Command Framework: https://github.com/aikar/commands
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"

    implementation ('net.silthus.configlib:configlib-bukkit:2.6.0') {
        exclude group: 'org.spigotmc', module: 'spigot-api'
    }

    // Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.mockito:mockito-core:4.6.1'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.14:0.2.2'
    testImplementation 'org.assertj:assertj-core:3.23.1'
}

shadowJar {
    classifier ""
    minimize()
    dependencies {
        include(dependency('co.aikar:acf-paper:0.5.1-SNAPSHOT'))
        include(dependency('net.silthus.configlib:configlib-bukkit:'))
        include(dependency('me.yushust.message:core:'))
        include(dependency('me.yushust.message:sourcetype-bukkit-yml:'))
    }
    relocate 'co.aikar.commands', "${packageName}.acf"
    relocate 'co.aikar.locales', "${packageName}.locales"
    relocate 'de.exlll.configlib', "${packageName}.configlib"
    relocate 'me.yushust.message', "${packageName}.nmessage"
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
    ignoreFailures = false
}

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

defaultTasks 'build'
