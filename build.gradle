buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'io.freefair.lombok' version '6.4.3'
    id 'java'
    id 'idea'
}

apply from: "$rootDir/gradle/publish.gradle"

if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.local.gradle")
}

sourceCompatibility = 8
targetCompatibility = 8

ext {
    mcVersion = project.property("mcVersion")
}

group project.property("group")

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

archivesBaseName = project.property("pluginName")

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:${mcVersion}-R0.1-SNAPSHOT")

    // Annotation Command Framework: https://github.com/aikar/commands
    implementation("co.aikar:acf-paper:0.5.1-SNAPSHOT")
    implementation("de.exlll:configlib-paper:4.5.0")
}

shadowJar {
    classifier("")
    minimize()
    relocate 'co.aikar.commands', "${packageName}.acf"
    relocate 'co.aikar.locales', "${packageName}.locales"
    relocate 'de.exlll.configlib', "${packageName}.configlib"
    relocate 'me.yushust.message', "${packageName}.nmessage"
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

defaultTasks 'build'
